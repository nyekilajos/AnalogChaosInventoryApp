project.ext.acAppVersion = '0.1'
def versionCodeTimeStamp = buildTime()
def versionNameForBuild = createVersionName()

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "hu.bme.simonyi.acstudio.analogchaosinventoryapp"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode versionCodeTimeStamp
        versionName "${acAppVersion}.${versionNameForBuild}"
        applicationId = "hu.bme.simonyi.acstudio.analogchaosinventoryapp"
    }
    signingConfigs {
        release {
            storeFile file(AC_STORE_FILE)
            storePassword AC_STORE_PASSWORD
            keyAlias AC_KEY_ALIAS
            keyPassword AC_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
            debuggable false
            resValue("string", "appversion", "${acAppVersion}.${versionNameForBuild}")
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled false
            debuggable true
            resValue("string", "appversion", "${acAppVersion}.${versionNameForBuild}")
        }
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

android.applicationVariants.all { variant ->
    def name
    variant.outputs.each { output ->
        def apkDirectory = output.packageApplication.outputFile.parentFile
        def additionalTags = ''
        if (variant.baseName.contains("debug")) {
            additionalTags += '-debug'
        }
        name = "ac-app${additionalTags}-${acAppVersion}.apk"
        output.packageApplication.outputFile = new File(apkDirectory, name);
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.+'
    compile 'org.roboguice:roboguice:3.0.1'
    provided 'org.roboguice:roboblender:3.+'
    compile 'org.springframework.android:spring-android-rest-template:2.0.0.M1'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.android.support:recyclerview-v7:21.0.+'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.github.bmelnychuk:atv:1.2.+'
    compile 'com.couchbase.lite:couchbase-lite-android:1.0.4'
    compile "com.fasterxml.jackson.core:jackson-core:2.5.4"
    compile "com.fasterxml.jackson.core:jackson-databind:2.5.4"
}

def buildTime() {
    def formattedDate = new Date().format('yyyMMdd')
    return formattedDate as int
}

String createVersionName() {
    def revisionCode = 'git rev-parse --short HEAD'.execute().text.trim()
    return (revisionCode != null && !revisionCode.isEmpty() ? "${revisionCode}" : "${versionCodeTimeStamp}") as String
}
